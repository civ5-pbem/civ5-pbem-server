buildscript {
    ext {
        springBootVersion = '2.1.3.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath('com.h2database:h2:1.4.196')
        classpath('org.yaml:snakeyaml:1.19')
    }
}

plugins {
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.2'
    id 'pl.allegro.tech.build.axion-release' version '1.10.0'
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'me.cybulski'
project.version = scmVersion.version
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

configurations {
    jacocoRuntime
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('org.springframework.boot:spring-boot-starter-security')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-mail')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.projectlombok:lombok')
    compile('org.liquibase:liquibase-core')
    compile('com.google.guava:guava:23.0')
    compile('commons-io:commons-io:2.6')
    runtime('com.h2database:h2')
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.springframework.security:spring-security-test')
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

scmVersion {
    // Dry run & local only release
    localOnly = true // never connect to remote
}

task versionNumber {
    doLast {
        new File("build/version.txt").withWriter { out -> out.println project.version }
    }
}
classes.dependsOn versionNumber

springBoot {
    buildInfo()
}
